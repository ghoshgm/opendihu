
include(CheckIncludeFile)
include(CheckSymbolExists)
include(CheckCXXSymbolExists)
include(ProcessorCount)

# Generic routine to set appropriate cores for all tests.
# Typically the tests are executed with maximum 2 cores.
cmake_host_system_information(RESULT Ncpu QUERY NUMBER_OF_PHYSICAL_CORES)
if(Ncpu LESS 2)
  ProcessorCount(n)
  if(n GREATER Ncpu)
    set(Ncpu ${n})
  endif()
  set(MPIEXEC_NUMPROC_MAX 1)
else()
  set(MPIEXEC_NUMPROC_MAX 2)
endif()

if(${MPI_FOUND})
  set(CMAKE_REQUIRED_LIBRARIES MPI::MPI_CXX)
  check_symbol_exists(MPI_Put mpi.h USE_MPI_PUT)
  check_symbol_exists(MPI_Win_lock mpi.h USE_MPI_WIN_LOCK)
  check_symbol_exists(MPI_Win_free mpi.h USE_MPI_WIN_FREE)
  check_symbol_exists(MPI_Win_unlock mpi.h USE_MPI_WIN_UNLOCK)
  check_symbol_exists(MPI_Win_create mpi.h USE_MPI_WIN_CREATE)
  check_symbol_exists(MPI_Win_allocate_shared mpi.h USE_MPI_ALLOC)
endif()

if(${precice_FOUND})
  set(CMAKE_REQUIRED_LIBRARIES precice::precice)
  check_cxx_symbol_exists(precice::constants::actionReadIterationCheckpoint SolverInterface.hpp HAVE_PRECICE)
endif()

if(${OpenBLAS_FOUND})
  set(CMAKE_REQUIRED_INCLUDES ${OpenBLAS_INCLUDE_DIRS})
  set(CMAKE_REQUIRED_LIBRARIES ${OpenBLAS_LIBRARIES})
  check_symbol_exists(cblas_dgemm cblas.h HAVE_CBLAS_DGEMM)
  check_symbol_exists(LAPACKE_dgesvd lapacke.h HAVE_LAPACKE_DGESVD)
  check_symbol_exists(LAPACKE_zgesvd lapacke.h HAVE_LAPACKE_ZGESVD)
  if(${HAVE_CBLAS_DGEMM} AND ${HAVE_LAPACKE_DGESVD} AND ${HAVE_LAPACKE_ZGESVD})
    set(HAVE_LAPACK 1)
  endif()
endif()

set(SIMD 0)
set(NDEBUG 0)
set(PAT 0)
set(EXTRAE 0)
set(CHASTE 0)

